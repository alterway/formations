

# Configuration Terraform

### Qu'est-ce que c'est ?

Une **configuration** Terraform est un fichier texte qui contient les d√©finitions des **ressources** d'infrastructure.  

Il est possible d'√©crire des configurations Terraform au format `HCL` (avec l'extension .tf) ou au format `JSON` (avec l'extension .tf.json).

### resources

- Les ressources sont les √©l√©ments de base d'une configuration Terraform. 

- Lorsque vous d√©finissez une configuration, vous d√©finissez une ou plusieurs (g√©n√©ralement plusieurs) ressources. 
- Les ressources sont sp√©cifiques au `provider`, de sorte qu'une ressource pour le provider AWS est diff√©rente d'une ressource pour OpenStack. 

- √âl√©ment le plus important dans Terraform

- Chaque ressource d√©crit un ou plusieurs √©l√©ments d‚Äôinfrastructure

- Plusieurs m√©ta-arguments
    - count
    - depends_on
    - for_each
    - provider
    - lifecycle
    - provisioner et connection


### Resources :Exemple

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}

resource "aws_instance" "web" {
  ami           = "ami-a1b2c3d4"
  instance_type = "t2.micro"
}

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


### Data Sources (1)

- Permettent √† Terraform d‚Äôutiliser des informations qu‚Äôil n‚Äôa pas d√©finit

- ü§î Chaque provider Terraform peut offrir ses propres data sources

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}
variable "vpc_id" {}

data "aws_vpc" "selected" {
  id = var.vpc_id
}

resource "aws_subnet" "example" {
  vpc_id            = data.aws_vpc.selected.id
  availability_zone = "us-west-2a"
  cidr_block        = cidrsubnet(data.aws_vpc.selected.cidr_block, 4, 1)
}

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}

data "github_repository_pull_requests" "pull_requests" {
  base_repository = "example-repository"
  base_ref        = "main"
  state           = "open"
}

module ‚Äúpreview-environment‚Äù {
  for_each        = data.github_repository_pull_requests.pull_requests.results
  name            = each.value.title
  commit_sha      = each.value.head_sha
  // ...
}
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

### Data Sources : Filtrer les data sources avec `filter` (1)

AWS : 

Les filtres permettent de faire le tri et de r√©cup√©rer les informations n√©cessaires, utiles pour les donn√©es externes.

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}

data "aws_ami" "example" {
  executable_users = ["self"]
  most_recent      = true
  name_regex       = "^myami-\\d{3}"
  owners           = ["self"]

  filter {
    name   = "name"
    values = ["myami-*"]
  }

  filter {
    name   = "root-device-type"
    values = ["ebs"]
  }

  filter {
    name   = "virtualization-type"
    values = ["hvm"]
  }
}
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

### Data Sources : Filtrer les data sources avec `filter` (2)

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}

data "aws_ami" "ubuntu" {
  most_recent = true
  owners      = ["099720109477"] # Canonical

  filter {
    name   = "virtualization-type"
    values = ["hvm"]
  }

  filter {
    name   = "architecture"
    values = ["x86_64"]
  }

  filter {
    name   = "image-type"
    values = ["machine"]
  }

  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-xenial-16.04-amd64-server-*"]
  }
}

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


### Data Sources : Filtrer les data sources avec `filter` (3)

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}

data "aws_ec2_transit_gateway" "tgw" {
  filter {
    name   = "tag:Name"
    values = ["wahlnetwork-tgw-prod"]
  }
}
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


### Cr√©er une configuration - Exercice

- Trouver la page de documentation Terraform de la ressource : aws_instance
 
- Copier le premier exemple dans un fichier `main.tf`

- Lancer la commande : `terraform init`

- Lancer la commande : `terraform plan`

- Lancer la commande `terraform apply`

- Voir le nouveau fichier cr√©√© `terraform.tfstate`

- Aller sur la console AWS ou Azure et v√©rifier que la ressource est cr√©√©e 



### Commentaires

Une ligne : `# or //` 


Multiple ligne : `/* ... */`

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}

/*
 This module creates a new AWS instance.
 If a VPC ID is not specified, a default VPC will be created.
...
*/

locals {
  # Common tags to be assigned to all resources
  common_tags = {
    Service = local.service_name
    Owner   = local.owner
  }
}
resource "aws_instance" "example" {
  # ...

  tags = local.common_tags
}
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

### Variables d‚Äôentr√©e (1)

12 factor app:
  Il doit y avoir une s√©paration stricte entre la `configuration` et le `code`

- Param√®tres pour personnaliser le code source

- D√©finition des valeurs √† l‚Äôaide :
    - Des options CLI
    - Des variables d‚Äôenvironnement

- Possibilit√© de d√©finir des r√®gles de validation personnalis√©es 


### Variables d‚Äôentr√©e (2)

- Types :
    - string
    - number
    - bool
    - list/tuple : `["us-west-1a", "us-west-1c"]` -  `["c", "b", "b"]`
    - map/object : `{name = "Mabel", age = 52}`
    - set : `["c", "b"]`  (Pas de duplication possible de valeurs) (fonction `toset()`)

### Variables d‚Äôentr√©e (3)

dans fichier tf :

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}

variable "rg_name" {
  type        = string
  description = "Resource Group Name - Must be unique in a subscription"
  default     = "a-terraform-training-01"
}

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Command line

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}
terraform plan/apply -var rg_name=a-terraform-training-03
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~



### Variables d‚Äôentr√©e (4)

Fichier de variables:

- Automatiques
    - `terraform.tfvars`
    - `*.auto.tfvars`
 

- Fichiers
  
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}
terraform apply -auto-approve -var-file=file-var.tfvars
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

- Variables d'environment
  
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}
TF_VAR_<var-name>="a-value" terraform plan
# eg.
TF_VAR_rg_name="a-terraform-training-10" terraform plan
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

### Variables ordre de lecture

1. dans le fichier `tf`
2. Variables d'environnement
3. terraform.tfvars
4. *.auto.tfvars
5. cmd line file vars (-var-file)
6. cmd line file var (-var)

### Variables : `string` par d√©faut

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}
variable "rg_name" {
 type        = string
 description = "Resource Group Name"
 default     = "a-terraform-training-01"
}
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

est √©galement √©quivalent √†

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}
variable "rg_name" {
 description = "Resource Group Name"
 default     = "a-terraform-training-01"
}
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

### Variables : `string` `heredoc`

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}
variable "app_description" {
 type        = string
 description = "application description"
 default     = <<EOT
Welcome !
Application version is %%app_version
EOT
}
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


### Variables : `number`

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}
variable "the_counter" {
 type        = number
 description = "# iteration"
 default     = 4
}
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


### Variables : `bool`

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}
variable "trueOrFalse" {
 type        = bool
 description = "true or false"
 default     = true
}
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

### Variables : `list`

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}
variable "eu_locations" {
 type        = list
 description = "location list"
 default     = ["westeurope","northeurope"]
}
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

### Variables : `map`

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}
variable "hel" {
 type        = map
 description = "hel person"
 default     = {
   surname = "Herv√©"
   name    = "Leclerc"
   role    = "CTO"
  }
}
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

### Variables : `object`

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}
variable "whoishel" {
  type        = object ({
    surname = string
    name    = string
    kids    = number
    skills  = list (string)
  }
 )
 description = "who is hel"
 default     = {
  surname = "Herv√©"
  name    = "Leclerc"
  kids    = 3
  skills  = [
              "kubernetes",
              "docker",
              "terraform"
            ]
  }
}
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


### Variables - Exercice

- Cr√©er le fichier variables.tf

- Cr√©er le fichier formation.tfvars

- Variabiliser :
    - la r√©gion
    - l‚Äôinstance type
    - tag Name


### Meta-arguments

- `depends_on` :
    - D√©pendance Explicite vs D√©pendance Implicite
    - depends_on = [‚Äúresource list‚Äù]
    - eg. depends_on = [ "azurerm_network_interface.nic1", "azurerm_managed_disk.dd1" ]


- `count` :
    - loop  count.index
    - Utile pour cr√©er une simple condition if then avec count = var.something si quelque-chose = 0 la ressource ne sera pas cr√©√©e/modifi√©e

- `provider`
    - Sp√©cifie le fournisseur √† utiliser pour une ressource. 
    - Ceci est utile lorsque on utilise plusieurs fournisseurs, ce qui est g√©n√©ralement utilis√© lorsque on cr√©e des ressources multir√©gionales. Pour diff√©rencier ces fournisseurs, vous utilisez un champ d'alias.

- `for_each` :
    - loop on a list or map
chaque instance de for_each a un identifiant unique lors de la cr√©ation ou de la modification de la configuration


- `lifecycle` :
    - Sur n'importe quel bloc
    - 3 arguments :
        - `create_before_destroy` (par d√©faut terraform d√©truire puis cr√©e)
        - `prevent_destroy` (terraform lancera une erreur si une ressource est d√©truite !! impossible d'utiliser terraform destroy)
        - `ignore_changes` (si quelque chose est modifi√© en externe, terraform ne modifiera pas la ressource)

### Meta-arguments Exemple (1)

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}

resource "azurerm_resource_group" "rg_1" {
 name     = var.rg_01
 location = var.location
 tags     = var.tags
 lifecycle  {
   create_before_destroy = true
 }
}
resource "azurerm_resource_group" "rg_2" {
 name     = var.rg_02
 location = var.location
 tags     = var.tags
 lifecycle  {
   prevent_destroy = true
 }
}
resource "azurerm_resource_group" "rg_3" {
 name     = var.rg_03
 location = var.location
 tags     = var.tags
 lifecycle  {
   ignore_changes = [ tags,]
 }
}

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ 


### Meta-arguments Exemple (2)

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}

## Default Provider
provider "google" {
  region = "us-central1"
}

## Another Provider
provider "google" {
  alias  = "europe"
  region = "europe-west1"
}

## Referencing the other provider
resource "google_compute_instance" "example" {
  provider = google.europe
}

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ 



### Outputs

- Valeurs de retour

- Utile pour exposer un sous-ensemble d‚Äôattributs de ressource √† un module parent. 

- Utile pour afficher certaines valeurs dans la sortie CLI apr√®s avoir ex√©cut√© terraform apply

- Dans le cas de backend distant, les outputs du module racine sont accessibles par d'autres configurations via une source de donn√©es terraform_remote_state

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}
## Output variable which will store the arn of instance 
## and display after terraform apply command.
output "ec2_arn" {
  ## Value depends on resource name and type (same as that of main.tf)
  value = aws_instance.my-machine.arn
}
## Output variable which will store instance public IP 
## and display after terraform apply command 
output "instance_ip_addr" {
  value       = aws_instance.my-machine.public_ip
  description = "The public IP address of the main server instance."
}
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


### Outputs - Exercice 

- Cr√©er le fichier outputs.tf

- Cr√©er l‚Äôouput ‚Äúpublic_dns‚Äù 

- Exposer la valeur de l‚Äôattribut public_dns

- Ex√©cuter la commande : terraform init

- Ex√©cuter la commande : terraform plan 

- Ex√©cuter la commande : terraform apply 

- Quelle commande affiche la valeur de l‚Äôoutput 


### Valeurs locales 

- Assigne un nom √† une expression 

- R√©utilisable dans la configuration

- Permet d‚Äô√©viter la r√©p√©tition 


~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}

locals {
  service_name  = "redis"
  resilient = true
}

locals {
  # Common tags to be assigned to all resources
  common_tags = {
    Service = local.service_name
    Owner   = local.owner
  }
}

locals {
  # Ids for multiple sets of EC2 instances, merged together
  instance_ids = concat(aws_instance.blue.*.id, aws_instance.green.*.id)
}

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

### Valeurs locales exemples


~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}

variable "project_name" {
  type = string
}
variable "environment" {
  type = string
}
locals {
  name-prefix = "${var.project_name}-${var.environment}"
}
resource "aws_s3_bucket" "default" {
  bucket = "${local.name-prefix}-bucket"
  acl    = "private"

  tags = {
    Name = "${local.name-prefix}-bucket"
  }
}

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~



### Fonctions int√©gr√©es (built-in) (1)

- Terraform inclut un nombre de fonctions int√©gr√©es

- Il n‚Äôest pas possible de cr√©er ses propres fonctions

- Familles de fonction 
    -  Num√©rique 
    -  Cha√Æne de caract√®res
    -  Collection
    -  Encodage 
    -  Syst√®me de fichier
    -  Date et heure
    -  Hachage et chiffrement 
    -  R√©seau
    -  Conversion de types 


### Fonctions int√©gr√©es (built-in) (2) : Quelques exemples

- numeric
    - abs, ceil, floor, log, max, min, parseint, pow, signum
- string
    - chomp, format, formatlist, indent, join, lower, regex, regexall, replace
    - split, strrev, substr, title, trim, trimprefix, trimsuffix, trimspace, upper
- collection
    - chunklist, coalesce, coalescelist, compact, concat, contains, distinct, element, flatten
    - index, keys, length, list, lookup, map, matchkeys, merge, range reverse, setintersection
    - setproduct, setsubtract, setunion, slice, sort, sum, transpose, values, zipmap
- encoding
    - base64decode, base64encode, base64gzip, csvdecode, jsondecode, jsonencode
    - urlencode, yamldecode, yamlencode
- filesystem
    - abspath, dirname, pathexpand, basename, file, fileexists, fileset, filebase64, templatefile
- date and time
    - formatdate, timeadd, timestamp
- hash and crypto
    - base64sha256,base64sha512, bcrypt, filebase64sha256, filebase64sha512, filemd5, filesha1, filesha256
    - filesha512, md5, rsadecrypt, sha1, sha256, sha512, uuid, uuidv5
- ip network
    - cidrhost, cidrnetmask, cidrsubnet, cidrsubnets

- type conversion
    - can, tobool, tolist, tomap, tonumber, toset, tostring, try


  
### Dynamic block

- Utilis√© pour construire dynamiquement des blocs imbriqu√©s reproductibles
- Fonctionne comme une boucle `for`

Pour faire des blocs dynamiques il faut :

```bash
Des "Collections"            :  list, map, set
Des "Iterator" (optionnel)   :  variable temporaire qui repr√©sente un √©l√©ment de la collection
Un "Content"                 :  Un bloc sur lequel on it√®re
```

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}
resource "aws_security_group" "example" {
  name = "example" # can use expressions here

  dynamic "ingress" {
    for_each = var.service_ports
    content {
      from_port = ingress.value
      to_port   = ingress.value
      protocol  = "tcp"
    }
  }
}
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}

resource "aws_security_group" "example" {
  name = "example" # can use expressions here

  dynamic "ingress" {
    for_each = var.service_ports
    iterator = "service_port"
    content {
      from_port = service_port.value
      to_port   = service_port.value
      protocol  = "tcp"
    }
  }
}
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

- (Voir les exemples dans le chapitre `loops`)



### Graphe des ressources

- Graphe des d√©pendances

- G√©n√©r√© par Terraform Core

- Construit √† partir des fichiers de configuration

- Utilis√© par Terraform pour : g√©rer les d√©pendances entre les ressources, la gestion du fichier d‚Äô√©tat‚Ä¶

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ {.zsh}

terraform graph

terraform graph | dot -Tsvg > graph.svg


~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


![](images/terraform/graph.png){height="200px"}



### Modules

Un module est un "conteneur" pour plusieurs ressources qui sont utilis√©es ensemble

Un chapitre est enti√®rement consacr√© aux un modules

![](images/terraform/terraform-module-structure.webp){height="300px"}


Avantanges des modules : 

- Organisation des configurations : facilite la compr√©hension des configurations
- Encapsulation : Permet de masquer l'impl√©mentationb interne de l'infrastructure et de se pr√©venir de changement non voulus 
- R√©-utilisation : Permet de cr√©er des modules qui sont utilis√©s dans plusieurs configurations
- Consistence : Permet de g√©rer facilement plusieurs environnements (staging, production, dev‚Ä¶)


### Interpolation

Une syntaxe terraform sp√©cifique pour r√©f√©rencer les attributs ou les arguments d'autres ressources (ou de soi-m√™me)

Une s√©quence `${ ... }` est une interpolation, qui √©value l'expression donn√©e entre les marqueurs, convertit le r√©sultat en une cha√Æne si n√©cessaire, puis l'ins√®re dans la cha√Æne finale :

```bash
"Hello, ${var.name}!"
```

